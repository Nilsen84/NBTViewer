plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'gg.essential.loom' version '0.10.0.4'
    id 'net.kyori.blossom' version '1.3.0'

    id 'gg.essential.defaults' version '0.1.11'
}

blossom {
    replaceToken '@NAME@', modName
    replaceToken '@ID@', modId
    replaceToken '@VERSION@', modVersion
}

group = 'me.onils'
version = modVersion

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()

    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed 'org.apache.commons:commons-text:1.9'

    embed 'gg.essential:loader-launchwrapper:1.1.3'
}

loom {
    launchConfigs {
        client {
            arg('--tweakClass', 'gg.essential.loader.stage0.EssentialSetupTweaker')
        }
    }
}


processResources {
    filesMatching('mcmod.info') {
        expand(
                'name': modName,
                'id': modId,
                'version': modVersion
        )
    }
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from(configurations.embed.collect { zipTree(it) })

    manifest.attributes(
            'ModSide': 'CLIENT',
            'ForceLoadAsMod': true,
            'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker',
            'TweakOrder': '0'
    )
}